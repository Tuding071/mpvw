apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext.abiCodes = ["armeabi-v7a": 1, "arm64-v8a": 2, "x86":3, "x86_64":4]
ext.universalBase = 8000

android {
    namespace 'is.xyz.mpv'
    compileSdk 35

    defaultConfig {
        minSdkVersion 21
        targetSdk 35
        versionCode 41
        versionName "2025-04-21-release"
        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "default"
    productFlavors {
        "default" {
            isDefault true
        }
        api29 {
            targetSdkVersion 29
            versionNameSuffix "-oldapi"
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    splits {
        abi {
            enable true
            reset()
            include("arm64-v8a") // Yalnızca arm64-v8a desteği
            universalApk false   // Universal APK oluşturma
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def base = project.ext.abiCodes.get(output.getFilter(VariantOutput.ABI))
        if (base != null)
            output.versionCodeOverride = base * 1000 + variant.versionCode
        else
            output.versionCodeOverride = project.ext.universalBase + variant.versionCode
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.media:media:1.7.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'
}
